buildscript {
    ext.kotlin_version = "1.5.20"

    repositories {
        google()
        mavenCentral()

        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.android.tools.build:gradle:4.2.2'
        classpath 'gradle.plugin.com.github.jlouns:gradle-cross-platform-exec-plugin:0.5.0'
        classpath "gradle.plugin.gmazzo:sqlite-plugin:0.2"
    }
}

plugins {
    id 'maven-publish'
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url "https://jitpack.io" }
        maven { url "https://datatrans.jfrog.io/artifactory/mobile-sdk/"}
    }

    project.ext {
        sdkVersion='0.40.1'
        versionCode=1

        compileSdkVersion=30
        minSdkVersion=21
        targetSdkVersion=30

        buildToolsVersion='30.0.2'
        okhttpVersion='4.9.1'
        desugarVersion='1.1.1'

        sdkVersion += project.getProperties().get("versionSuffix", "")
    }
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name = "LocalBuildDir"
                def releasesRepoUrl = "file://"+ project.rootDir.absolutePath +"/build/maven-releases/"
                def snapshotsRepoUrl = "file://"+ project.rootDir.absolutePath +"/build/maven-snapshots/"
                url = rootProject.sdkVersion.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }

            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/snabble/Android-SDK")

                credentials {
                    username = System.getenv("GITHUB_ACTOR")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }
}

task printVersion() {
    println project.sdkVersion
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
