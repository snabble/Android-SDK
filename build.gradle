buildscript {
    ext {
        compose_version = '1.3.0-beta01'
    }
    ext.kotlin_version = '1.6.21'

    repositories {
        google()
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-base:$kotlin_version"
        classpath 'com.android.tools.build:gradle:7.2.2'
        classpath 'gradle.plugin.com.github.jlouns:gradle-cross-platform-exec-plugin:0.5.0'
        classpath 'gradle.plugin.gmazzo:sqlite-plugin:0.2'
        classpath 'com.github.bjoernq:unmockplugin:0.7.9'
    }
}

plugins {
    id 'maven-publish'
    id 'org.jetbrains.dokka'
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://datatrans.jfrog.io/artifactory/mobile-sdk/' }
    }

    project.ext {
        sdkVersion='0.68.6'
        versionCode=1

        compileSdkVersion=31
        minSdkVersion=21
        targetSdkVersion=31

        okhttpVersion='4.10.0'
        desugarVersion='1.1.5'

        sdkVersion += project.getProperties().get('versionSuffix', '')
    }
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name = 'LocalBuildDir'
                def releasesRepoUrl = 'file://' + project.rootDir.absolutePath + '/build/maven-releases/'
                def snapshotsRepoUrl = 'file://' + project.rootDir.absolutePath + '/build/maven-snapshots/'
                url = rootProject.sdkVersion.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }

            maven {
                name = 'GitHubPackages'
                url = uri('https://maven.pkg.github.com/snabble/Android-SDK')

                credentials {
                    username = System.getenv('GITHUB_ACTOR')
                    password = System.getenv('GITHUB_TOKEN')
                }
            }
        }
    }
}

tasks.named("dokkaHtml").configure {
    dokkaSourceSets {
        configureEach {
            skipDeprecated.set(true)
        }
    }
}

tasks.withType(org.jetbrains.dokka.gradle.AbstractDokkaTask).configureEach {
    outputDirectory.set(rootDir.toPath().resolve("docs").toFile())
    suppressObviousFunctions.set(true)
    moduleName.set("snabble Android SDK API Documentation")
    def escapedLogoPath = file("dokka/assets/logo-icon.svg").absolutePath.replaceAll("\\\\", "\\\\\\\\")
    pluginsMapConfiguration.set(["org.jetbrains.dokka.base.DokkaBase":
    """
        {
            "footerMessage": "Â© 2022 snabble GmbH",
            "customAssets": [
                "${escapedLogoPath}"
            ]
        }
    """])
}

task printVersion() {
    println project.sdkVersion
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
