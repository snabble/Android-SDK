task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

version = project.sdkVersion
group = 'io.snabble.sdk'

afterEvaluate {
    publishing {
        publications {
            "$project.name"(MavenPublication) {
                from project.components.release
                artifact androidSourcesJar

                groupId = 'io.snabble.sdk'
                artifactId = project.name
                version = project.sdkVersion

                pom {
                    url = 'https://github.com/snabble/Android-SDK'
                    licenses {
                        license {
                            name = 'The MIT License'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }

                    withXml {
                        asNode().dependencies.'*'.findAll() {
                            it.scope.text() == 'runtime' && project.configurations.implementation.allDependencies.find { dep ->
                                dep.name == it.artifactId.text()
                            }
                        }.each { it.scope*.value = 'implementation'}
                    }
                }
            }
        }
    }
}
