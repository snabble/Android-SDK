apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion project.compileSdkVersion
    buildToolsVersion project.buildToolsVersion

    defaultConfig {
        applicationId 'io.snabble.testapp'
        minSdkVersion project.minSdkVersion
        targetSdkVersion project.targetSdkVersion
        versionCode 2
        versionName '1.0'

        // Setup here or in your local.properties file you Snabble secrets
        def appId = null
        def endpoint = null
        def secret = null

        def configFile = project.rootProject.file('local.properties')
        if ((appId == null || endpoint == null || secret == null) && configFile.exists()) {
            Properties properties = new Properties()
            properties.load(configFile.newDataInputStream())

            appId = properties.getProperty('snabble.appId')
            endpoint = properties.getProperty('snabble.endpoint')
            secret = properties.getProperty('snabble.secret')
        }
        resValue 'string', 'app_id', appId ?: '<missing app id>'
        resValue 'string', 'endpoint', endpoint ?: ''
        resValue 'string', 'secret', secret ?: '<missing secret>'

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
        debug {
            minifyEnabled false
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        freeCompilerArgs = ["-Xjvm-default=all"]
        jvmTarget = "1.8"
    }
}

dependencies {
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:${project.desugarVersion}"

    // just for simplicity
    implementation project(':core')
    implementation project(':ui')
    implementation project(':ui-integration')

    // in your app you need to use those dependencies:
    //implementation 'io.snabble.sdk:core:0.50.6'
    //implementation 'io.snabble.sdk:ui:0.50.6'
    //implementation 'io.snabble.sdk:ui-integration:0.50.6'

    //noinspection GradleDependency
    implementation 'commons-io:commons-io:2.5'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}