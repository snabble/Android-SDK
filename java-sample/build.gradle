apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion project.compileSdkVersion

    defaultConfig {
        applicationId 'io.snabble.testapp'
        minSdkVersion project.minSdkVersion
        targetSdkVersion project.targetSdkVersion
        versionCode 2
        versionName '1.0'

        def appId = "snabble-sdk-demo-app-oguh3x"
        def endpoint = "https://api.snabble.io"
        def secret = "2TKKEG5KXWY6DFOGTZKDUIBTNIRVCYKFZBY32FFRUUWIUAFEIBHQ===="

        def configFile = project.rootProject.file('local.properties')
        if (configFile.exists()) {
            Properties properties = new Properties()
            properties.load(configFile.newDataInputStream())

            appId = properties.getProperty('snabble.appId', appId)
            endpoint = properties.getProperty('snabble.endpoint', endpoint)
            secret = properties.getProperty('snabble.secret', secret)
        }

        manifestPlaceholders = [
                appId: appId,
                endpoint: endpoint,
                secret: secret,
        ]

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
        debug {
            minifyEnabled false
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        freeCompilerArgs = ["-Xjvm-default=all"]
        jvmTarget = "1.8"
    }
}

dependencies {
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:${project.desugarVersion}"

    implementation project(':core')
    implementation project(':ui')

    // in your app you need to use those dependencies:
    // implementation 'io.snabble.sdk:core:{currentVersion}'
    // implementation 'io.snabble.sdk:ui:{currentVersion}'

    implementation 'androidx.appcompat:appcompat:1.4.1'

    //noinspection GradleDependency
    implementation 'commons-io:commons-io:2.5'
    implementation 'com.google.android.material:material:1.6.1'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}