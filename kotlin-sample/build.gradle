apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 31

    defaultConfig {
        applicationId 'io.snabble.sdk.sample'
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 1
        versionName '1.0'

        // Setup here or in your local.properties file you Snabble secrets
        def appId = null
        def endpoint = null
        def secret = null

        def configFile = project.rootProject.file('local.properties')
        if ((appId == null || endpoint == null || secret == null) && configFile.exists()) {
            Properties properties = new Properties()
            properties.load(configFile.newDataInputStream())

            appId = properties.getProperty('snabble.appId')
            endpoint = properties.getProperty('snabble.endpoint')
            secret = properties.getProperty('snabble.secret')
        }
        resValue 'string', 'app_id', appId ?: '<missing app id>'
        resValue 'string', 'endpoint', endpoint ?: ''
        resValue 'string', 'secret', secret ?: '<missing secret>'

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        freeCompilerArgs = ['-Xjvm-default=all']
        jvmTarget = '1.8'
    }
}

dependencies {
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:${project.desugarVersion}"

    implementation project(':core')
    implementation project(':ui')

    // in your app you need to use those dependencies:
    // implementation 'io.snabble.sdk:core:{currentVersion}'
    // implementation 'io.snabble.sdk:ui:{currentVersion}'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}