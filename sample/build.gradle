apply plugin: 'com.android.application'
apply plugin: 'com.github.gmazzo.sqlite'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion project.compileSdkVersion
    buildToolsVersion project.buildToolsVersion

    defaultConfig {
        applicationId "io.snabble.testapp"
        minSdkVersion project.minSdkVersion
        targetSdkVersion project.targetSdkVersion
        versionCode 2
        versionName "1.0"

        Properties properties = new Properties()
        def localFile = project.rootProject.file('local.properties')
        if (localFile.exists()) {
            properties.load(localFile.newDataInputStream())

            def id = properties.getProperty('snabble.appId')
            def endpoint = properties.getProperty('snabble.endpoint')
            def secret = properties.getProperty('snabble.secret')
            
            resValue "string", "app_id", id != null ? id : "<missing app id>"
            resValue "string", "endpoint", endpoint != null ? endpoint : ""
            resValue "string", "secret", secret != null ? secret : "<missing secret>"
        } else {
            resValue "string", "app_id", "<missing app id>"
            resValue "string", "endpoint", ""
            resValue "string", "secret", "<missing secret>"
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            minifyEnabled false
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        freeCompilerArgs = ["-Xjvm-default=all"]
        jvmTarget = "1.8"
    }
}

dependencies {
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:${project.desugarVersion}"

    implementation project(':core')
    implementation project(':ui')
    implementation project(':ui-integration')

    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'commons-io:commons-io:2.5'
    implementation 'com.google.android.material:material:1.3.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}
